plugins {
	id 'com.gladed.androidgitversion' version '0.4.14'
}

apply plugin: 'com.android.application'

androidGitVersion {
	codeFormat = "MNNPPBBBB"
	format = "%tag%%-count%%-branch%%-dirty%"
	prefix = "v"  // Only tags beginning with v are considered.
	untrackedIsDirty = false
}

dependencies {
	// 1.2.0 is the newest version we can use that won't complain about minSdk version.
	implementation "androidx.appcompat:appcompat:1.6.1"

	// Convenient wrapper around DocumentContract. Might look into writing our own
	// to see if there's some performance to squeeze at some point, but doubt it.
	implementation "androidx.documentfile:documentfile:1.1.0"
}

android {
	flavorDimensions "variant"
	namespace = 'org.ppsspp.ppsspp'
	signingConfigs {
		debug {
			storeFile file("debug.keystore")
		}
		optimized {
			storeFile file("debug.keystore")
		}

		// Set these in a system global (or project local, but not checked in) gradle.properties .
		if (project.hasProperty("RELEASE_STORE_FILE")) {
			release {
				storeFile file(RELEASE_STORE_FILE)
				storePassword RELEASE_STORE_PASSWORD
				keyAlias RELEASE_KEY_ALIAS
				keyPassword RELEASE_KEY_PASSWORD
			}
		}
	}

	compileSdk = 36

	ndkVersion = "28.2.13676358"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}

	lint {
		baseline = file("lint-baseline.xml")
	}

	defaultConfig {
		applicationId 'org.ppsspp.ppsspp'
		if (androidGitVersion.name() != "unknown") {
			println "Overriding Android Version Name, Code: " + androidGitVersion.name() + " " + androidGitVersion.code()
			versionName androidGitVersion.name()
			versionCode androidGitVersion.code()
		} else {
			println "(not using these:) Android Version Name, Code: " + androidGitVersion.name() + " " + androidGitVersion.code()
		}
		file("versionname.txt").text = androidGitVersion.name()
		file("versioncode.txt").text = androidGitVersion.code().toString()

		minSdk = 21
		targetSdk = 36
		if (project.hasProperty("ANDROID_VERSION_CODE") && project.hasProperty("ANDROID_VERSION_NAME")) {
			versionCode = ANDROID_VERSION_CODE
			versionName = ANDROID_VERSION_NAME
		}
		signingConfig = signingConfigs.debug
	}
	buildTypes {
		debug {
			minifyEnabled = false
			jniDebuggable = true
			signingConfig = signingConfigs.debug
		}
		optimized {
			// Debug signed but optimized.
			minifyEnabled = false
			jniDebuggable = true
			signingConfig = android.buildTypes.debug.signingConfig
		}
		release {
			minifyEnabled = false
			signingConfig = signingConfigs.release
		}
	}
	externalNativeBuild {
		cmake {
			path '../CMakeLists.txt'
		}
	}
	packagingOptions {
		jniLibs.useLegacyPackaging = true
	}
	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			res.srcDirs = ['res']
			java.srcDirs = ['src']
			aidl.srcDirs = ['src']
			resources.srcDirs = ['src']
			assets.srcDirs = [
				'../assets',
			]
		}
		normal {
			res.srcDirs = ['normal/res']
		}
		gold {
			res.srcDirs = ['gold/res']
		}
		vr {
			res.srcDirs = ['normal/res']
			manifest.srcFile 'VRManifest.xml'
		}
		legacy {
			res.srcDirs = ['legacy/res']
		}
	}
	productFlavors {
		normal {
			applicationId 'org.ppsspp.ppsspp'
			dimension "variant"
			externalNativeBuild {
				cmake {
					// Available arguments listed at https://developer.android.com/ndk/guides/cmake.html
					arguments '-DANDROID=true',
						'-DANDROID_PLATFORM=android-21',
						'-DANDROID_TOOLCHAIN=clang',
						'-DANDROID_CPP_FEATURES=',
						'-DANDROID_STL=c++_static'
				}
			}
			ndk {
				abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
			}
		}
		gold {
			applicationId 'org.ppsspp.ppssppgold'
			dimension "variant"
			externalNativeBuild {
				cmake {
					// Available arguments listed at https://developer.android.com/ndk/guides/cmake.html
					arguments '-DANDROID=true',
						'-DANDROID_PLATFORM=android-21',
						'-DANDROID_TOOLCHAIN=clang',
						'-DANDROID_CPP_FEATURES=',
						'-DANDROID_STL=c++_static',
						'-DANDROID_ARM_NEON=TRUE',
						'-DGOLD=TRUE'
				}
			}
			ndk {
				abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
			}
		}
		legacy {
			applicationId 'org.ppsspp.ppsspplegacy'
			dimension "variant"
			// targetSdk is overridden in androidComponents below.
			externalNativeBuild {
				cmake {
					// Available arguments listed at https://developer.android.com/ndk/guides/cmake.html
					arguments '-DANDROID=true',
						'-DANDROID_PLATFORM=android-21',
						'-DANDROID_TOOLCHAIN=clang',
						'-DANDROID_CPP_FEATURES=',
						'-DANDROID_STL=c++_static',
						'-DANDROID_LEGACY=TRUE'
				}
			}
			ndk {
				//noinspection ChromeOsAbiSupport
				abiFilters 'armeabi-v7a', 'arm64-v8a'
			}
		}
		vr {
			applicationId 'org.ppsspp.ppssppvr'
			dimension "variant"
			// targetSdk is overridden in androidComponents below.
			externalNativeBuild {
				cmake {
					// Available arguments listed at https://developer.android.com/ndk/guides/cmake.html
					arguments '-DANDROID=true',
						'-DANDROID_PLATFORM=android-21',
						'-DANDROID_TOOLCHAIN=clang',
						'-DANDROID_CPP_FEATURES=',
						'-DANDROID_STL=c++_static',
						'-DANDROID_ARM_NEON=TRUE',
						'-DOPENXR=TRUE',
						'-DANDROID_LEGACY=TRUE'
				}
			}
			ndk {
				//noinspection ChromeOsAbiSupport
				abiFilters 'arm64-v8a'
			}
		}
	}
	buildFeatures {
		aidl = true
		buildConfig = true
	}
}

androidComponents {
	beforeVariants(selector().all()) { variantBuilder ->
		def needed = variantBuilder.name in [
			'normalDebug',      // for debugging
			'normalOptimized',  // for testing/user build
			'normalRelease',    // for Google Play releases
			'goldDebug',
			'goldRelease',      // for Google Play releases
			'vrDebug',          // for VR debugging
			'vrOptimized',      // for VR testing
			'vrRelease',        // for VR releases
			'legacyDebug',      // for legacy debugging
			'legacyOptimized',  // for legacy testing/user build
			'legacyRelease',    // for buildbot releases
		]
		variantBuilder.enable = needed

		// Override targetSdk for legacy and VR variants
		if (variantBuilder.flavorName.contains("legacy")) {
			variantBuilder.targetSdk = 29
		} else if (variantBuilder.flavorName.contains("vr")) {
			variantBuilder.targetSdk = 29
		}
	}
}

afterEvaluate {
	android.sourceSets.main.assets.getSrcDirs().each { println it }
}
